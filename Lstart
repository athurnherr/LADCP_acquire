#!/usr/bin/expect -f
#======================================================================
#                    L S T A R T 
#                    doc: Sun Mar  7 16:18:07 2021
#                    dlm: Mon Apr 12 13:37:58 2021
#                    (c) 2021 A.M. Thurnherr
#                    uE-Info: 49 51 NIL 0 0 72 2 2 4 NIL ofnI
#======================================================================

# HISTORY:
#  	Mar  9, 2021: - created 
#  	Mar 10, 2021: - made work
#	Mar 19, 2021: - improved
#   Mar 31, 2021: - BUG: profnum 036 was interpreted as octal
#	Apr 12. 2021: - added 30s timeout with NO default to erase memory question

source [regsub {Ldir} [exec which Ldir] {libacquire2.expect}];

log_user 0;
init_acquire;

if {![file isfile .last_LADCP]} {							# set LADCP profile id
	exec echo 0 > .last_LADCP;
}
set profnum [expr [scan [exec cat .last_LADCP] %d] + 1];
send_user "LADCP profile number = $profnum\n";

if ![file isdirectory $DATA_DIRECTORY] {					# ensure data directory exists
	exec mkdir -p $DATA_DIRECTORY; 
}

set cast_dir [format %s/%03d $DATA_DIRECTORY $profnum];		# sanity check
if [file isdirectory $cast_dir] {
	send_error "ABORT: data directory $cast_dir already exists, correct .last_LADCP to proceed\n";
	exit 1;
}
	
if {$argc == 0} {											# collect CTD info (stn.cast)
	set stncast [input_stncast "CTD Station.Cast = "];
} else {
	set stncast [lindex $argv 0];
}

send_user "Connecting...";									# wake up instruments
connect_to_instruments;
wakeup_instruments;

list_dir;													# erase memory if needed/requested
if {!$memory_empty && [affirm "\nERASE MEMORY? (y/N) " 30]} {
	send_user "Erasing memory...";
	erase_memory;
	send_user "\n";
}
	
exec mkdir $cast_dir;										# create data directory
exec echo $profnum  > .last_LADCP;							# save current LADCP profile number

exec cp $PRIMARY_SETUP $cast_dir;							# save instrument configurations
if {[info exists SECONDARY_COMMS]} {
	exec cp $SECONDARY_SETUP $cast_dir;
}
exec echo $stncast > [format %s/CTD.station $cast_dir];		# save CTD station/cast info

send_user "Resetting instruments to factory defaults...";	# load factory defaults
load_factory_defaults;

send_user "\nSetting instrument clocks...";					# set instrument  clocks
set_clocks;

send_user "\nUploading command files";
upload_configs;

send_user "\nDone\n";

